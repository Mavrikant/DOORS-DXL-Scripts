// This script copies links in source object to destination object.
/*
  This script copies links in source object to destination object.

  In order to use this script, folder containing linked objects should be opened
*/

//**********************************  History  ******************************
// Caner Bayram       2022-05-01  Initial version
// M. Serdar Karaman  2022-05-13  Script turned into GUI
// M. Serdar Karaman  2022-05-31  Refactoring, Null problem is solved. 
// M. Serdar Karaman  2023-06-14  Missing current module parameter is added.
// 
//***********************************  TODO  ********************************
// Copy in-links
// Link selection GUI for copy

DBE sourceIDField
DBE destinationIDField

void CopyLinks(DB db)
{
  string sourceID = get(sourceIDField)
  string destinationID = get(destinationIDField)

  Object srcObj = object(intOf(sourceID),(current Module))
  Object dstObj = object(intOf(destinationID),(current Module))

  if (srcObj == null)
  {
    print("Source object is not found")
    return
  }

  if ( dstObj == null)
  {
    print("Destination object is not found")
    return
  }

  LinkModuleDescriptor linkModDes = null
  for linkModDes in (current Folder) do
  {
    string linkMod = getName(linkModDes)
    Link myLink = null
    for myLink in srcObj -> linkMod do
    {
      ModName_ targetMod = target(myLink)
      read(fullName(targetMod), false)
      Object targetObj = target(myLink)
      dstObj -> linkMod -> targetObj 
    }
  }
}

//////////////////////// GUI PROGRAM ////////////////////////
string moduleName = (current Module).(NLS_("Name"))
DB db = create (moduleName " - Copy Links",  styleCentered | styleThemed | styleAutoparent)

sourceIDField = field(db, "Source: " moduleName "_", "", 50) 
destinationIDField = field(db, "Destination: " moduleName "_", "", 50) 
apply(db, "Copy", CopyLinks)
show (db)
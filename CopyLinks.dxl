// This script copies links in source object to destination object.
/*
  This script copies links in source object to destination object.

  In order to use this script, folder containing linked objects should be opened
*/

//**********************************  History  ******************************
// Caner Bayram       2022-05-01  Initial version
// M. Serdar Karaman  2022-05-13  Script turned into GUI
// M. Serdar Karaman  2022-05-31  Refactoring, Null problem is solved. 

//***********************************  TODO  ********************************
// Memory leak analysis
// Copy in-links
// Link selection GUI for copy

Folder currFolder = current
DBE sourceIDField = null
DBE destinationIDField = null
DB db = null

void CopyLinks(DB db)
{
  Object srcObj = object(intOf(get(sourceIDField)))
  Object dstObj = object(intOf(get(destinationIDField)))

  LinkModuleDescriptor linkModDes = null
  for linkModDes in currFolder do
  {
    string linkMod = getName(linkModDes)
    Link myLink = null
    for myLink in srcObj -> linkMod do
    {
      ModName_ targetMod = target(myLink)
      read(fullName(targetMod), false)
      Object targetObj = target(myLink)
      dstObj -> linkMod -> targetObj 
    }
  }
}

//////////////////////// GUI PROGRAM ////////////////////////
string moduleName = (current Module).(NLS_("Name"))
db = create (moduleName " - Copy Links",  styleCentered | styleThemed | styleAutoparent)

sourceIDField = field(db, "Source: " moduleName "_", "", 50) 
destinationIDField = field(db, "Destination: " moduleName "_", "", 50) 
apply(db, "Copy", CopyLinks)
show (db)
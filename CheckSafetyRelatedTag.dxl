
// This script check safety related tag on requirements against its upper requirement.
/*
  This script check safety related tag on requirements against its upper requirement.
  Author: M. Serdar Karaman
*/

//**********************************  History  ******************************
// M. Serdar Karaman  2024-07-18  Initial version
// M. Serdar Karaman  2025-01-14  Initial version

pragma runLim, 0

Module currMod = current
filtering off

Object o = null
for o in currMod do 
{
    if ((o."Req" "" == "N") || (o."Req" "" == ""))
    {
        continue
    }
    
    Link outLink = null // used to get Outlinks

    bool isSafetyRelated = false
    bool isPIDS_ICDLinkFound = false

    for outLink in (o)->"*" do
    {
        ModName_ targetMod = target(outLink)
        int start = 0
        int end = 0
        bool caseSensitive = false

        bool found1 = findPlainText(name(targetMod), "PIDS", start, end, caseSensitive)
        if (found1)
        {
            isPIDS_ICDLinkFound = true
            read(fullName(targetMod), false)
            
            Object targetObj = target(outLink)
            if (targetObj."Safety Related" "" == "Yes")
            {
                isSafetyRelated = true
                break
            }
        }

        bool found2 = findPlainText(name(targetMod), "ICD", start, end, caseSensitive)
        if (found2)
        {
            isPIDS_ICDLinkFound = true
            read(fullName(targetMod), false)
            
            Object targetObj = target(outLink)
            if (targetObj."Safety Related" "" == "Yes")
            {
                isSafetyRelated = true
                break
            }
        }


    }
    
    if (isPIDS_ICDLinkFound == true)
    {
        if ((isSafetyRelated == true) and ((o."Safety Requirement" "" == "No") or (o."Safety Requirement" "" == "False")))
        {
            print identifier(o) " marked as No but traced PIDS/ICD links are safety Yes\n"
        }

        if ((isSafetyRelated == false) and ((o."Safety Requirement" "" == "Yes") or (o."Safety Requirement" "" == "True")))
        {
            print identifier(o) " marked as Yes but traced PIDS/ICD links are safety No\n"
        }
    }
    else
    {
        print identifier(o) " marked as " o."Safety Requirement" " without PIDS/ICD  link\n"
    }

}